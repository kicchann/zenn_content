---
title: "Google TTSã§Powerpointã«éŸ³å£°ãƒ•ã‚¡ã‚¤ãƒ«ã‚’æ·»ä»˜ã™ã‚‹"
emoji: "ğŸ“¢"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["googletts", "powerpoint", "vba", "windows"]
published: false
published_at: 2099-12-31 23:59 # æœªæ¥ã®æ—¥æ™‚ã‚’æŒ‡å®šã™ã‚‹
---

## æ¦‚è¦

Microsoft365ã®Powerpointã¯å‹•ç”»ä½œæˆã‚‚å®¹æ˜“ã«ãªã£ã¦ãŠã‚Šï¼Œç´ æ™´ã‚‰ã—ã„ã®ã§ã™ãŒï¼ŒåŸºæœ¬çš„ã«ã¯äººãŒã—ã‚ƒã¹ã‚‹å‰æã«ãªã£ã¦ãŠã‚Šï¼Œãƒ†ã‚­ã‚¹ãƒˆã‹ã‚‰éŸ³å£°ãƒ‡ãƒ¼ã‚¿ã‚’ä½œæˆã™ã‚‹ã“ã¨ã¯ã§ãã¾ã›ã‚“ï¼  

ãƒ—ãƒ¬ã‚¼ãƒ³ãŒè‹¦æ‰‹ãªäººã‚„ï¼ŒåŒ¿åã§å‹•ç”»ä½œæˆã—ãŸã„äººã«ã¨ã£ã¦ã¯ï¼Œè¨˜è¿°ã—ãŸãƒ†ã‚­ã‚¹ãƒˆã‚’éŸ³å£°å¤‰æ›ã—ã¦ï¼Œã‚µã‚¯ãƒƒã¨ãƒ—ãƒ¬ã‚¼ãƒ³ç”¨éŸ³å£°ã‚„å‹•ç”»ä½œæˆã¨ã—ã¦æ´»ç”¨ã—ãŸã„ã®ã§ã¯ãªã„ã‹ï¼Œã¨æ€ã„ï¼ŒGoogleTTSã‚’ã‚¢ãƒ‰ã‚¤ãƒ³ã¨ã—ã¦å®Ÿè£…ã—ã¾ã—ãŸã®ã§ç´¹ä»‹ã—ã¾ã™ï¼  
  
ã“ã‚“ãªæ„Ÿã˜ã§ãƒ¯ãƒ³ã‚¯ãƒªãƒƒã‚¯ã§ï¼Œã‚¹ãƒ©ã‚¤ãƒ‰ã”ã¨ã«è¨­ã‘ã‚‰ã‚Œã¦ã„ã‚‹ãƒãƒ¼ãƒˆã‚’éŸ³å£°ã«å¤‰æ›ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ï¼

![](../images/0001_google_tts/sample.gif)

ãƒªãƒœãƒ³ã‚‚å®Ÿè£…ã—ã¦ã„ã¾ã—ã¦ï¼Œå…¥åŠ›ã‚¿ã‚¤ãƒ—ï¼ˆãƒ†ã‚­ã‚¹ãƒˆã‹SSMLã‹ï¼‰ï¼ŒéŸ³å£°ã®æ€§åˆ¥ï¼Œç™ºè©±ã®é€Ÿåº¦ï¼Œè¨€èªã‚’é¸æŠã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ï¼

![](../images/0001_google_tts/ribbon.png)

ã“ã‚Œãªã‚‰ä½•å›ã‚‚ã‚„ã‚Šç›´ã™ã“ã¨ã¯ã§ãã¾ã™ã—ï¼Œä½•ã‚ˆã‚ŠGoogleTTSã¯ç„¡æ–™æ ãŒå¤§ãã„ã®ã§ï¼Œæ™®é€šã®ä½¿ã„æ–¹ã‚’ã—ã¦ã„ã‚Œã°**ã»ã¼ç„¡æ–™**ã§åˆ©ç”¨ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ï¼

## è©³ç´°

åŸºæœ¬çš„ã«ã¯Rest APIã«ãƒªã‚¯ã‚¨ã‚¹ãƒˆã‚’æŠ•ã’ã¦ï¼Œçµæœã‚’Powerpointã«è²¼ã‚Šä»˜ã‘ã¦ã„ã‚‹ã ã‘ã§ã™ï¼  
ãŸã ãã‚Œã ã¨å‘³æ°—ãªã„ã®ã§ãã‚Œãã‚Œèª¬æ˜ã—ã¦ã„ãã¾ã™ï¼

### Google TTS

[Google Text-to-Speech](https://cloud.google.com/text-to-speech)  
  
Rest APIã®ãƒªãƒ•ã‚¡ãƒ¬ãƒ³ã‚¹ã¯[ã“ã¡ã‚‰](https://cloud.google.com/text-to-speech/docs/reference/rest/v1/text/synthesize)ã§ã™ï¼  
  
vbaã§ãƒªã‚¹ã‚¨ã‚¹ãƒˆã—ã¦ï¼Œå–å¾—ã™ã‚‹ã“ã¨ã«ãªã‚‹ã®ã§ã™ãŒï¼Œã„ãã¤ã‹èª²é¡ŒãŒã‚ã‚Šã¾ã™ï¼
  
ã¾ãšï¼Œvbaã«ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§jsonãƒ‡ãƒ¼ã‚¿ã‚’ä½œæˆã™ã‚‹ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãŒå­˜åœ¨ã—ã¾ã›ã‚“ï¼  
ã§ã™ã®ã§è‡ªä½œã™ã‚‹å¿…è¦ãŒã‚ã‚‹ã®ã§ã™ãŒï¼Œå¥‡ç‰¹ãªäººãŒã„ã¦ï¼ŒJsonConverterãªã‚‹ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãŒGitHubã§å…¬é–‹ã•ã‚Œã¦ã„ã¾ã™ï¼
  
https://github.com/VBA-tools/VBA-JSON  
  
ã“ã‚Œã§payloadã‚’ä½œæˆã—ã¦ï¼ŒAPIã«æŠ•ã’ã¾ã™ï¼  
  
ã¾ãŸï¼ŒéŸ³å£°ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ç›´æ¥å—ã‘å–ã‚‹ã“ã¨ãŒã§ãã¾ã›ã‚“ï¼  
ãã“ã§ï¼Œãƒªãƒ‹ã‚¢ PCMï¼ˆ LINEAR16 ï¼‰ã¨å‘¼ã°ã‚Œã‚‹æ–‡å­—åˆ—ã§å—ã‘å–ã‚Šï¼Œãã‚Œã‚’ãƒã‚¤ãƒŠãƒªã«å¤‰æ›ã—ï¼Œwavãƒ•ã‚¡ã‚¤ãƒ«ã¨ã—ã¦å–å¾—ã™ã‚‹ã“ã¨ã«ã—ã¾ã—ãŸï¼

ä»¥ä¸‹ã«APIã«ã‚¢ã‚¯ã‚»ã‚¹ã™ã‚‹ã‚³ãƒ¼ãƒ‰ã‚’æ²è¼‰ã—ã¾ã™ï¼

```vb:Google_TTS.bas
Public Function GoogleTextToSpeech( _
    ByRef myNote As String, _
    ByRef inputType As String, _
    ByRef name As String, _
    ByRef language As String, _
    ByRef speakingRate As Single, _
    ByRef pitch As Single _
    ) As Variant

    'APIã¨ã‚„ã‚Šå–ã‚Šã™ã‚‹ãŸã‚ã®ãƒ‡ãƒ¼ã‚¿ã‚’Dictionaryå‹ã§ä½œã‚‹ï¼ˆå¾Œã§JSONã«å¤‰æ›ï¼‰
    'audioConfig
    Dim AudioConfig As Object
    Set AudioConfig = New Dictionary
    AudioConfig.Add "audioEncoding", "LINEAR16"
    AudioConfig.Add "speakingRate", speakingRate
    AudioConfig.Add "pitch", pitch

    'input
    Dim AudioInput As Object
    Set AudioInput = New Dictionary
    AudioInput.Add inputType, myNote

    'voice
    Dim AudioVoice As Object
    Set AudioVoice = New Dictionary
    AudioVoice.Add "languageCode", language
    AudioVoice.Add "name", name

    'Jsonã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚’ä½œæˆ
    Dim JsonObject As Object
    Set JsonObject = New Dictionary
    JsonObject.Add "audioConfig", AudioConfig
    JsonObject.Add "input", AudioInput
    JsonObject.Add "voice", AudioVoice

    'APIå®Ÿè¡Œ
    Dim objHTTP As Object
    Dim uri As String
    Dim response As Variant
    Dim res As Object
    Dim Choices As Variant
    Set objHTTP = CreateObject("msxml2.xmlhttp")
    uri = GOOGLE_TEXT_TO_SPEECH_API_URI & "?key=" & API_KEY
    objHTTP.Open "POST", uri, False
    objHTTP.setRequestHeader "Content-Type", "application/json"
    objHTTP.send JsonConverter.ConvertToJson(JsonObject)

    'ãƒ¬ã‚¹ãƒãƒ³ã‚¹ã‚³ãƒ¼ãƒ‰ã®ãƒã‚§ãƒƒã‚¯(æ­£å¸¸)
    If objHTTP.Status <> 200 Then
        GoogleTextToSpeech = False
        Exit Function
    End If

    response = objHTTP.responseText

    'ãƒ¬ã‚¹ãƒãƒ³ã‚¹JSONã‚’è¾æ›¸å‹ã«å¤‰æ›ã™ã‚‹
    Set res = ParseJson(response)
    'ãƒ¬ã‚¹ãƒãƒ³ã‚¹ã®ä¸­ã‹ã‚‰è³ªå•ã¸ã®å›ç­”ã‚’å–ã‚Šå‡ºã—ã¦ã€æˆ»ã‚Šå€¤ã¨ã™ã‚‹
    Choices = res("audioContent")
    GoogleTextToSpeech = Choices
End Function
```

### ã‚¹ãƒ©ã‚¤ãƒ‰ã¸ã®éŸ³å£°åŸ‹ã‚è¾¼ã¿

æ‰‹å‹•ã§ã‚¹ãƒ©ã‚¤ãƒ‰ã«éŸ³å£°ãƒ‡ãƒ¼ã‚¿ã‚’åŸ‹ã‚è¾¼ã‚€æ‰‹é †ã¨åŒæ§˜ã«ï¼Œãƒ•ã‚¡ã‚¤ãƒ«ã®å¤–ã‹ã‚‰éŸ³å£°ãƒ•ã‚¡ã‚¤ãƒ«ã‚’åŸ‹ã‚è¾¼ã‚€å¿…è¦ãŒã‚ã‚Šã¾ã™ï¼  
ãã“ã§ï¼Œä¸€æ—¦ä¸€æ™‚ãƒ•ã‚¡ã‚¤ãƒ«ã«éŸ³å£°ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ã¦ï¼Œãã®ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ã‚¹ãƒ©ã‚¤ãƒ‰å†…ã«åŸ‹ã‚è¾¼ã¿ï¼Œå®Œäº†ã—ãŸã‚‰ä¸€æ™‚ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å‰Šé™¤ã—ã¦ã„ã¾ã™ï¼  
  
ä»¥ä¸‹ã«å…¨ä½“ã®æµã‚Œã‚’æ²è¼‰ã—ã¾ã™ï¼

```vb
Private Function VbaTextToSpeech( _
    ByRef myNote As String, _
    Optional inputType As String = "text", _
    Optional gender As String = "male", _
    Optional speakingRate As String = "normal", _
    Optional pitch As Single = 0#, _
    Optional language As String = "ja-JP" _
    )
    Dim name As String
    Dim rate As Single

    name = getName(gender, language)
    rate = getRate(speakingRate)

    VbaTextToSpeech = Google_TTS.GoogleTextToSpeech(myNote, inputType, name, language, rate, pitch)
End Function

Public Sub EmbedVoice()
    On Error GoTo MyErr

    ' ç¾åœ¨ã®ã‚¹ãƒ©ã‚¤ãƒ‰ç•ªå·ã‚’å–å¾—
    Dim slideIndex As Integer
    slideIndex = ActiveWindow.Selection.SlideRange.slideIndex

    ' ç¾åœ¨ã®ã‚¹ãƒ©ã‚¤ãƒ‰ã®ãƒãƒ¼ãƒˆã‚’å–å¾—
    Dim myNote As String
    myNote = ActivePresentation.Slides(slideIndex).NotesPage.Shapes.Placeholders(2).TextFrame.TextRange.Text

    ' ä¸€æ™‚ãƒ•ã‚©ãƒ«ãƒ€ã‚’å–å¾—
    Dim tempPath As String
    tempPath = Environ("TEMP")

    ' wavãƒ•ã‚¡ã‚¤ãƒ«ã®ãƒ‘ã‚¹ã‚’ä½œæˆ
    Dim wavePath As String
    wavePath = tempPath & "\voice.wav"

    ' å®Ÿè¡Œå‰ã®ç¢ºèª
    If MyMessages.askToRun = vbNo Then
        Exit Sub
    End If

    ' wavãƒ•ã‚¡ã‚¤ãƒ«ãŒã‚¹ãƒ©ã‚¤ãƒ‰å†…ã«æ—¢ã«ã‚ã‚Œã°å‰Šé™¤
    Dim fso As Object
    Set fso = CreateObject("Scripting.FileSystemObject")
    If fso.FileExists(wavePath) Then Kill wavePath

    ' éŸ³å£°ã®æ–‡å­—åˆ—ãƒ‡ãƒ¼ã‚¿ã‚’å–å¾—
    Dim output As Variant
    output = VbaTextToSpeech( _
        myNote, _
        ribbonInputType, _
        ribbonGender, _
        ribbonSpeakingRate, _
        ribbonPitch, _
        ribbonLanguage _
    )
    If output = False Then
        Call MyMessages.failToRunTextToSpeech
        Exit Sub
    End If

    ' wavãƒ•ã‚¡ã‚¤ãƒ«ã«ä¿å­˜
    If decodeBase64(output, wavePath) = False Then
        Call MyMessages.failToDecode
        Exit Sub
    End If

    ' audioã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®åŸ‹ã‚è¾¼ã¿
    Dim oSlide As Slide
    Dim oShp As Shape
    Dim oEffect As Effect

    With ActivePresentation.Slides(slideIndex).Shapes.AddMediaObject2(wavePath, False, True, 10, 10)
        With .AnimationSettings.PlaySettings
            .PlayOnEntry = True
            .HideWhileNotPlaying = True
        End With
    End With

    ' wavãƒ•ã‚¡ã‚¤ãƒ«ã®å‰Šé™¤
    fso.DeleteFile wavePath

    Call MyMessages.success
    Exit Sub

MyErr:
    Call MyMessages.unknown
End Sub
```

## ãƒªãƒœãƒ³

ãƒªãƒœãƒ³ã¯Custom UI Editor Toolã§ã‚«ã‚¹ã‚¿ãƒã‚¤ã‚ºã—ã¾ã—ãŸï¼

[Custom UI Editorã®æœ€æ–°ç‰ˆãŒGitHubã§å…¬é–‹ã•ã‚Œã¾ã—ãŸã€‚](https://www.ka-net.org/blog/?p=8945)

ã‚«ã‚¹ã‚¿ãƒ ã—ãŸãƒªãƒœãƒ³ã®å€¤ã¯ï¼ŒPowerpointã‚’ç«‹ã¡ä¸Šã’ãŸéš›ã«ç´ä»˜ã‘ã¾ã™ï¼

```vb:CustomUI.bas
Option Explicit

#If VBA7 And Win64 Then
   Private Declare PtrSafe Sub MoveMemory Lib "kernel32" Alias "RtlMoveMemory" (pDest As Any, pSrc As Any, ByVal cbLen As LongPtr)
 #Else
   Private Declare Sub MoveMemory Lib "kernel32" Alias "RtlMoveMemory" (pDest As Any, pSrc As Any, ByVal cbLen As Long)
 #End If

Private RibbonUI As IRibbonUI

Public ribbonInputType As String
Public ribbonGender As String
Public ribbonSpeakingRate As String
Public ribbonPitch As Single
Public ribbonLanguage As String

Public Sub ribbon_onLoad(ribbon As IRibbonUI)
    
    Set RibbonUI = ribbon
    SaveSetting "RibbonApp", "Main", "RibbonPointer", CStr(ObjPtr(ribbon)) 'ãƒªãƒœãƒ³ã®ãƒã‚¤ãƒ³ã‚¿ã‚’ãƒ¬ã‚¸ã‚¹ãƒˆãƒªã«è¨˜éŒ²
    Dim rCtrl As IRibbonControl
    
    ribbonInputType = "text"
    ribbonGender = "male"
    ribbonSpeakingRate = "normal"
    ribbonPitch = 0#
    ribbonLanguage = "ja-JP"
    
    RibbonUI.Invalidate
End Sub

Private Sub SampleTab_getVisible(control As IRibbonControl, ByRef returnedVal)
    'å¸¸ã«ã‚¿ãƒ–ã‚’è¡¨ç¤ºã™ã‚‹
    returnedVal = True
End Sub

Public Sub ResetRibbon()
 #If VBA7 And Win64 Then
    Set RibbonUI = GetRibbon(CLngPtr(GetSetting("RibbonApp", "Main", "RibbonPointer")))
  #Else
    Set RibbonUI = GetRibbon(CLng(GetSetting("RibbonApp", "Main", "RibbonPointer")))
  #End If
End Sub

#If VBA7 And Win64 Then
 Private Function GetRibbon(ByVal lRibbonPointer As LongPtr) As Object
   Dim p As LongPtr
 #Else
 Private Function GetRibbon(ByVal lRibbonPointer As Long) As Object
   Dim p As Long
 #End If
   Dim ribbonObj As Object
   MoveMemory ribbonObj, lRibbonPointer, LenB(lRibbonPointer)
   Set GetRibbon = ribbonObj
   p = 0: MoveMemory ribbonObj, p, LenB(p)
End Function
```

ã‚¤ãƒ™ãƒ³ãƒˆæ¤œçŸ¥ã¯ã“ã‚“ãªæ„Ÿã˜ã§ã™ï¼

```vb:Group.bas
Option Explicit

Public Sub btn_googleTTS_onAction(control As IRibbonControl)
    On Error GoTo MyErr
    Call Module_TTS.EmbedVoice
    Exit Sub

MyErr:
    Call ResetRibbon
End Sub

Public Sub ddnInputType_getSelectedItemID(control As IRibbonControl, ByRef returnedVal)
  returnedVal = ribbonInputType
End Sub

Public Sub ddnInputType_onAction(control As IRibbonControl, id As String, index As Integer)
    On Error GoTo MyErr
    ribbonInputType = id
MyErr:
    Call ResetRibbon
End Sub

Public Sub ddnGender_getSelectedItemID(control As IRibbonControl, ByRef ReturnValue)
    ReturnValue = ribbonGender
End Sub

Public Sub ddnGender_onAction(control As IRibbonControl, id As String, index As Integer)
    On Error GoTo MyErr
    ribbonGender = id
MyErr:
    Call ResetRibbon
End Sub

Public Sub ddnSpeakingRate_getSelectedItemID(control As IRibbonControl, ByRef ReturnValue)
    ReturnValue = ribbonSpeakingRate
End Sub

Public Sub ddnSpeakingRate_onAction(control As IRibbonControl, id As String, index As Integer)
    On Error GoTo MyErr
    ribbonSpeakingRate = id
MyErr:
    Call ResetRibbon
End Sub

Public Sub ddnLanguage_getSelectedItemID(control As IRibbonControl, ByRef ReturnValue)
    ReturnValue = ribbonLanguage
End Sub

Public Sub ddnLanguage_onAction(control As IRibbonControl, id As String, index As Integer)
    On Error GoTo MyErr
    ribbonLanguage = id
MyErr:
    Call ResetRibbon
End Sub
```

Google TTSã‚’ä½¿ã†ã¨ã¨ã¦ã‚‚è‡ªç„¶ãªéŸ³å£°ã‚’ç”Ÿæˆã§ãã‚‹ã®ã§ï¼Œã¨ã¦ã‚‚ãŠã™ã™ã‚ã§ã™ï¼
å‚è€ƒã«ãªã‚Œã°å¹¸ã„ã§ã™ï¼